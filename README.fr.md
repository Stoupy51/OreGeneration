
### Translations
* [English](https://github.com/Stoupy51/SmartOreGeneration/blob/main/README.md)
* [Fran√ßaise](https://github.com/Stoupy51/SmartOreGeneration/blob/main/README.fr.md)


# üìñ Smart Ore Generation
Librairie Minecraft sous forme de data pack pour g√©rer la g√©n√©ration de minerais custom en utilisant un syst√®me intelligent pour la position des minerais.
* Cette librairie est un compl√©ment au syst√®me de g√©n√©ration de minerai vanilla. Elle ne le remplace pas.
* Elle a √©t√© con√ßue pour √™tre compatible avec d'autres librairies de g√©n√©ration de minerai.
* Cette librairie ne fournit qu'un moyen de g√©n√©rer des minerais de mani√®re intelligente, elle ne fournit pas de fonctions de pour l'impl√©mentation de custom blocks.
* Vous pouvez configurer la librairie pour g√©n√©rer des minerais dans une dimension sp√©cifique, ou dans toutes les dimensions.
* Elle fonctionne avec une hauteur minimale personnalis√©e de l'overworld, un score constant est calcul√© pour l'overworld uniquement si besoin.
* Cette librairie supporte les dimensions customs, mais vous devez les ajouter au function tag `denied_dimensions` pour les emp√™cher d'√™tre scann√©es si vous ne voulez pas qu'elles le soient.
* La librairie fonctionnera m√™me si le monde a √©t√© g√©n√©r√© avant l'installation de celle-ci car elle ne d√©pend pas de la g√©n√©ration du monde.

Il s'agit d'une librairie int√©gr√©e que vous int√©grez dans votre Datapack au lieu d'avoir √† le t√©l√©charger s√©par√©ment. N√©cessite [LanternLoad](https://github.com/LanternMC/load) pour fonctionner.

<br>

# üìö Explication du syst√®me
Toutes les 2 secondes, la librairie ex√©cute √† l'emplacement de chaque joueur les √©tapes suivantes :
* Ex√©cuter le function tag `denied_dimensions` pour vous permettre de refuser que certaines dimensions soient scann√©es.
* Si la dimension est autoris√©e, 8 r√©gions autour des joueurs sont scann√©es. (La taille par d√©faut des r√©gions est 96x96)
* Pour chaque r√©gion, la librairie va v√©rifier si elle a d√©j√† √©t√© scann√©e. Si non, elle lancera la g√©n√©ration de minerai pour cette r√©gion.
* Ensuite, le function tag `generate_ores` est appel√© √† l'origine de la r√©gion (0, 0, 0).
* Vous pouvez ajouter votre propre fonction de g√©n√©ration de minerai √† ce function tag o√π vous g√©n√©rerez vos minerais en fonction de conditions que vous avez choisies. (comme les dimensions, les biomes, etc...)
* Pour chaque filon de minerai que vous g√©n√©rez, vous devez utiliser le function tag de position al√©atoire `random_position` pour obtenir une position al√©atoire √† l'int√©rieur de la r√©gion. Ce function tag d√©placera la position vers une position al√©atoire et au bloc d'air le plus proche. En proc√©dant ainsi, vous √©viterez que les minerais soient g√©n√©r√©s √† l'int√©rieur des blocs et ne soient jamais trouv√©s par les joueurs (ce qui ajoutera du lag au serveur si le minerai utilise une entit√©). Ainsi, les minerais seront plus faciles √† trouver.
* Et apr√®s chaque v√©rification, la librairie sauvegardera la r√©gion comme "scann√©e" pour √©viter qu'elle ne soit scann√©e √† nouveau en ajoutant un `barrier block` √† l'origine de la r√©gion.
* Enfin, le fonction tag `post_generation` est appel√© pour vous permettre de faire quelques t√¢ches post-g√©n√©ration, comme lancer une fonction sur tous vos minerais nouvellement g√©n√©r√©s pour leur ajouter des tags nbt ou autre chose.

<br>

# üîß Function Tag
## üì• <ins>Signals</ins>
### `#smart_ore_generation:v1/denied_dimensions`
* Ce function tag est appel√© lorsque la librairie veut scanner une r√©gion. Vous pouvez ajouter quelles dimensions que vous voulez emp√™cher d'√™tre scann√©es en ajoutant une fonction √† ce tag.
* Pour utiliser ce signal, vous devez ajouter une fonction √† la liste de tags situ√©e dans `data/smart_ore_generation/tags/functions/v1/signals/denied_dimensions.json`.
* Reportez-vous √† ce mod√®le pour le contenu de la fonction [ici](https://github.com/Stoupy51/SmartOreGeneration/blob/main/data/smart_ore_generation/functions/v1.0/signals/denied_dimensions.mcfunction).
```mcfunction
#> smart_ore_generation:v1.0/signals/denied_dimensions
#
# @within			#smart_ore_generation:v1/signals/denied_dimensions
# @executed			as & at le joueur qui a d√©clench√© le signal
# @input score		#authorized smart_ore_generation.data √©gal √† 1 ou 0 si un autre datapack a d√©j√† refus√© la g√©n√©ration
#
## WARNING!
# - Vous devez v√©rifier que le score #authorized vaut 1 avant d'ex√©cuter des commandes pour √©viter de r√©activer la g√©n√©ration dans une dimension qui est d√©sactiv√©e par un autre datapack.
# - Mettre le score #authorized √† 0 d√©sactivera la g√©n√©ration dans la dimension pour tous les datapacks.

## Voici un exemple pour d√©sactiver la g√©n√©ration dans une dimension :
execute if score #authorized smart_ore_generation.data matches 1 if predicate simplenergy:in_overworld run scoreboard players set #authorized smart_ore_generation.data 0

## Autre exemple:
execute if score #authorized smart_ore_generation.data matches 1 if dimension minecraft:overworld run scoreboard players set #authorized smart_ore_generation.data 0
```
### `#smart_ore_generation:v1/generate_ores`
* Ce function tag est appel√© lorsque la librairie veut g√©n√©rer des minerais dans une r√©gion. Vous pouvez ajouter votre propre fonction de g√©n√©ration de minerai √† ce tag.
* Pour utiliser ce signal, vous devez ajouter une fonction √† la liste de tags situ√©e dans `data/smart_ore_generation/tags/functions/v1/signals/generate_ores.json`.
* R√©f√©rez-vous √† ce mod√®le pour le contenu de la fonction [ici](https://github.com/Stoupy51/SmartOreGeneration/blob/main/data/smart_ore_generation/functions/v1.0/signals/example/generate_ores.mcfunction)
```mcfunction
#> smart_ore_generation:v1.0/signals/generate_ores
#
# @within			#smart_ore_generation:v1/signals/generate_ores
# @executed			as un marker sp√©cial & at une position dont vous ne devriez pas vous soucier
#
## WARNING!
# - Vous ne devez pas utiliser /kill @s dans ce fichier, ou la librairie enti√®re cessera de fonctionner.
# - N'oubliez pas de modifier les scores #min_height et #max_height avant d'essayer de g√©n√©rer des minerais.
#
# @example from SimplEnergy datapack
# Cet exemple g√©n√®re 4 filons de Simplunium Ore dans l'overworld uniquement.
# Par r√©gion et entre le point le plus bas du monde (minecraft par d√©faut : -64) et y=40

# Dimension score, 0 = overworld
scoreboard players set #dimension smart_ore_generation.data -1
execute if predicate simplenergy:in_overworld run scoreboard players set #dimension smart_ore_generation.data 0

# Generate Simplunium Ore (x4) in the overworld only
scoreboard players operation #min_height smart_ore_generation.data = _OVERWORLD_BOTTOM smart_ore_generation.data
scoreboard players set #max_height smart_ore_generation.data 40
execute if score #dimension smart_ore_generation.data matches 0 run function smart_ore_generation:v1.0/signals/example/simplunium_ore
execute if score #dimension smart_ore_generation.data matches 0 run function smart_ore_generation:v1.0/signals/example/simplunium_ore
execute if score #dimension smart_ore_generation.data matches 0 run function smart_ore_generation:v1.0/signals/example/simplunium_ore
execute if score #dimension smart_ore_generation.data matches 0 run function smart_ore_generation:v1.0/signals/example/simplunium_ore
# Voir le mod√®le dans le lien pour le contenu de la fonction smart_ore_generation:v1.0/signals/example/simplunium_ore.mcfunction
```
### `#smart_ore_generation:v1/post_generation`
* Ce function tag est appel√© lorsque la librairie a fini de g√©n√©rer des minerais dans les r√©gions. Vous pouvez ajouter votre propre fonction de post-g√©n√©ration √† ce tag.
* Pour utiliser ce signal, vous devez ajouter une fonction √† la liste de tags situ√©e dans `data/smart_ore_generation/tags/functions/v1/signals/post_generation.json`.
* R√©f√©rez-vous √† ce mod√®le pour le contenu de la fonction [ici](https://github.com/Stoupy51/SmartOreGeneration/blob/main/data/smart_ore_generation/functions/v1.0/signals/example/post_generation.mcfunction)
```mcfunction
#> smart_ore_generation:v1.0/signals/post_generation
#
# @within			#smart_ore_generation:v1/signals/post_generation
# @executed			as none at none (default of a /schedule)
#
# Cette fonction est ex√©cut√©e une fois apr√®s la g√©n√©ration de toutes les r√©gions.
# Par exemple, si 6 r√©gions sont g√©n√©r√©es, cette fonction sera ex√©cut√©e 1 fois apr√®s la g√©n√©ration de toutes les r√©gions.
# Elle peut √™tre utilis√©e pour effectuer certaines t√¢ches post-g√©n√©ration, comme ex√©cuter une fonction sur tous les minerais que vous avez g√©n√©r√©
# au lieu de l'ex√©cuter sur chaque minerai individuellement lorsque vous le g√©n√©rez.
# Utile pour l'optimisation.
#
# @example from SimplEnergy datapack
# Cet exemple va √©diter les nbt des entit√©s pour les simplunium ore et deepslate simplunium ore
# si ils ont √©t√© g√©n√©r√©s tout au long du processus de g√©n√©ration.
# Ceci est utile pour l'optimisation car vous n'ex√©cutez la fonction "secondary" qu'une seule fois
# au lieu de l'ex√©cuter sur chaque minerai individuellement lorsque vous le g√©n√©rez.

# Place simplunium ore if it has been generated
execute if score #generated_ore simplenergy.data matches 1 as @e[tag=simplenergy.new_simplunium_ore] at @s run function simplenergy:place/simplunium_ore/secondary
execute if score #generated_ore simplenergy.data matches 1 run scoreboard players reset #generated_ore simplenergy.data

# Place deepslate simplunium ore if it has been generated
execute if score #generated_deepslate_ore simplenergy.data matches 1 as @e[tag=simplenergy.new_deepslate_simplunium_ore] at @s run function simplenergy:place/deepslate_simplunium_ore/secondary
execute if score #generated_deepslate_ore simplenergy.data matches 1 run scoreboard players reset #generated_deepslate_ore simplenergy.data
```
## üì§ <ins>Slots</ins>
### `#smart_ore_generation:v1/slots/random_position`
* Ce function tag doit √™tre appel√© lorsque vous avez besoin d'une position al√©atoire dans la r√©gion.
* Pour utiliser correctement ce slot, voir le mod√®le [ici].(https://github.com/Stoupy51/SmartOreGeneration/blob/main/data/smart_ore_generation/functions/v1.0/signals/example/simplunium_ore.mcfunction)
```mcfunction
#> smart_ore_generation:v1.0/signals/example/simplunium_ore
#
# @example from SimplEnergy datapack
# Exemple de comment trouver une position al√©atoire dans une r√©gion pour g√©n√©rer un minerai
# Toujours lancer le function tag de position al√©atoire avant tout
# @TIP: La position al√©atoire n'est pas plate, elle a un chiffre apr√®s la virgule.
# Vous pouvez utiliser ce chiffre pour g√©n√©rer le minerai d'une certaine mani√®re comme ci-dessous
# Il va essayer de placer le filon de minerai en fonction du chiffre
# donc sur chaque ligne ci-dessous, certaines peuvent √©chouer rendant cela plus r√©aliste
# r√©sultant en un filon de minerai contenant entre 2 et 6 minerais (dans cet exemple)

## Essayer de trouver une position al√©atoire adjacente √† l'air dans la r√©gion pour g√©n√©rer le minerai.
function #smart_ore_generation:v1/slots/random_position

# Placing Simplunium Ore Patch
execute at @s if block ~ ~ ~ #simplenergy:for_simplunium_ore run function simplenergy:calls/smart_ore_generation/simplunium_type
execute at @s positioned ~0.8 ~0.8 ~0.8 if block ~ ~ ~ #simplenergy:for_simplunium_ore run function simplenergy:calls/smart_ore_generation/simplunium_type
execute at @s positioned ~0.0 ~0.8 ~0.8 if block ~ ~ ~ #simplenergy:for_simplunium_ore run function simplenergy:calls/smart_ore_generation/simplunium_type
execute at @s positioned ~0.8 ~0.8 ~0.0 if block ~ ~ ~ #simplenergy:for_simplunium_ore run function simplenergy:calls/smart_ore_generation/simplunium_type
execute at @s positioned ~0.8 ~0.0 ~0.8 if block ~ ~ ~ #simplenergy:for_simplunium_ore run function simplenergy:calls/smart_ore_generation/simplunium_type
execute at @s positioned ~0.0 ~0.8 ~0.0 if block ~ ~ ~ #simplenergy:for_simplunium_ore run function simplenergy:calls/smart_ore_generation/simplunium_type
```

<br>

## Comment l'utiliser ?
1. Utilisez un datapack merger : [Mitochrondria Online](https://mito.thenuclearnexus.live/)
2. Impl√©mentez l'API comme d√©crit ci-dessus.

OU

1. Installez [LanternLoad](https://github.com/LanternMC/load) dans votre data pack
2. Copiez le dossier `data/smart_ore_generation` dans votre data pack
3. Fusionnez le contenu de `SmartOreGeneration/data/load/tags/functions/load.json` et votre `data/load/tags/functions/load.json`
4. Impl√©mentez l'API comme d√©crit ci-dessus.

